CMAKE_MINIMUM_REQUIRED(VERSION 3.5)

IF(NOT LwIP_DIR)
	MESSAGE(FATAL_ERROR "LwIP_DIR not specified.")
ENDIF()

SET(LwIP_SRC_DIR ${LwIP_DIR}/src)
SET(LwIP_SYSTEM_DIR ${LwIP_DIR}/system)
SET(LwIP_INC_DIR ${LwIP_SRC_DIR}/include)

FILE(GLOB LwIP_API_FILES ${LwIP_SRC_DIR}/api/*.c)
FILE(GLOB LwIP_CORE_FILES ${LwIP_SRC_DIR}/core/*.c)
FILE(GLOB LwIP_IPv4_FILES ${LwIP_SRC_DIR}/core/ipv4/*.c)
IF(ipv6 IN_LIST LwIP_FIND_COMPONENTS)
	FILE(GLOB LwIP_IPv6_FILES ${LwIP_SRC_DIR}/core/ipv6/*.c)
ENDIF()

FILE(GLOB LwIP_NETIF_FILES ${LwIP_SRC_DIR}/netif/*.c)

IF(ppp IN_LIST LwIP_FIND_COMPONENTS)
	FILE(GLOB LwIP_PPP_FILES ${LwIP_SRC_DIR}/netif/ppp/*.c)
ENDIF()

IF(system IN_LIST LwIP_FIND_COMPONENTS)
	FILE(GLOB LwIP_SYSTEM_SOURCES ${LwIP_SYSTEM_DIR}/OS/*.c)
	SET(LwIP_SYSTEM_INCLUDES ${LwIP_SYSTEM_DIR})
ENDIF()

SET(LwIP_INCLUDES ${LwIP_INC_DIR})

SET(LwIP_APPS altcp_tls httpd lwiperf mdns mqtt netbiosns smtp snmp sntp tftp)

FOREACH(LwIP_APP ${LwIP_APPS})
	IF(${LwIP_APP} IN_LIST LwIP_FIND_COMPONENTS)
		SET(LwIP_APPS_USED 1)
		SET(LwIP_${LwIP_APP}_DIR ${LwIP_SRC_DIR}/apps/${LwIP_APP})
		FILE(GLOB LwIP_${LwIP_APP}_SRCS ${LwIP_${LwIP_APP}_DIR}/*.c)
		LIST(APPEND LwIP_APPS_SOURCES ${LwIP_${LwIP_APP}_SRCS})
	ENDIF()
ENDFOREACH()

IF(LwIP_APPS_USED)
	LIST(APPEND LwIP_APPS_INCLUDES ${LwIP_INC_DIR}/lwip/apps)
	LIST(APPEND LwIP_APPS_INCLUDES ${LwIP_SRC_DIR}/apps)
ENDIF()

# Remove httpd/fsdata.c because it's included using #include, so compiling it will cause multiple definition errors.
# This line does nothing when httpd is not used
LIST(REMOVE_ITEM LwIP_APPS_SOURCES ${LwIP_SRC_DIR}/apps/httpd/fsdata.c)

SET(LwIP_SOURCES
	${LwIP_API_FILES}
	${LwIP_CORE_FILES}
	${LwIP_IPv4_FILES}
	${LwIP_IPv6_FILES}
	${LwIP_NETIF_FILES}
	${LwIP_PPP_FILES}
	${LwIP_SYSTEM_SOURCES}
	${LwIP_APPS_SOURCES}
)

SET(LwIP_INCLUDE_DIRS
	${LwIP_INCLUDES}
	${LwIP_SYSTEM_INCLUDES}
	${LwIP_APPS_INCLUDES}
)

INCLUDE(FindPackageHandleStandardArgs)

FIND_PACKAGE_HANDLE_STANDARD_ARGS(LwIP DEFAULT_MSG LwIP_INCLUDE_DIRS LwIP_SOURCES)
